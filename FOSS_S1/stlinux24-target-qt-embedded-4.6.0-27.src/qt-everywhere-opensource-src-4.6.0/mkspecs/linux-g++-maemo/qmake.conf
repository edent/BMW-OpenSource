#
# qmake configuration for Maemo 5 & 6
#

MAKEFILE_GENERATOR      = UNIX
TEMPLATE                = app
CONFIG                 += qt warn_on release incremental link_prl
QT                     += core gui
QMAKE_INCREMENTAL_STYLE = sublib

include(../common/g++.conf)
include(../common/linux.conf)

# Work round scratchbox not having standard paths in it's default search path
QMAKE_LFLAGS    += -Wl,-rpath-link=/lib
QMAKE_LFLAGS    += -Wl,-rpath-link=/usr/lib

# Override the default lib/include directories for scratchbox:
QMAKE_INCDIR_X11      = /usr/include/X11
QMAKE_INCDIR_OPENGL   = /usr/include
QMAKE_LIBDIR_X11      = /usr/lib
QMAKE_LIBDIR_OPENGL   = /usr/lib

# We still need to generate debug symbols in release mode to put into the *-dbg packages:
QMAKE_CFLAGS_RELEASE    += -g -fno-omit-frame-pointer -fno-optimize-sibling-calls
QMAKE_CXXFLAGS_RELEASE  += -g -fno-omit-frame-pointer -fno-optimize-sibling-calls

# Work round PowerVR SGX 1.3 driver bug with glScissor & FBOs:
DEFINES += QT_GL_NO_SCISSOR_TEST


load(qt_config)
